<?xml version="1.0" encoding="UTF-8"?><!-- -*- xml -*- -->
<xsl:stylesheet
    version="1.0" 
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:tei="http://www.tei-c.org/ns/1.0"
    xmlns="http://www.w3.org/1999/xhtml"
    xmlns:exsl="http://exslt.org/common"
    xmlns:php="http://php.net/xsl"
    extension-element-prefixes="exsl php"
    xmlns:data="urn:data"
    exclude-result-prefixes="tei data"
    >

  <!--****** xslt/index.xsl
 * NAME
 * index.xsl
 * SYNOPSIS
 * Templates for creating an index page for the public.
 * AUTHOR
 * Swithun Crowe
 * CREATION DATE
 * 20120710
 * SEE ALSO
 *   * xslt/params.xsl
 *   * xslt/templates.xsl
 *   * xslt/paging.xsl
 *   * xslt/facets.xsl
 *   * xslt/hits.xsl
 ****-->
  
  <xsl:include href="params.xsl"/>
  <xsl:include href="templates.xsl"/>
  <xsl:include href="paging.xsl"/>
  <xsl:include href="facets.xsl"/>
  <xsl:include href="hits.xsl"/>

  <xsl:template match="response">
    
    <!-- show unused facets -->
    <xsl:if test="$haveQuery or $haveFacets">
      <div id="facets_left">
            <xsl:apply-templates select="exsl:node-set($params)/data:params/data:param[@solr]" mode="facet_list"/>
            </div>
    </xsl:if>
    
     <!-- used facets -->
    <xsl:if test="$haveFacets or $haveQuery">
      <ul id="used_fields">
        <xsl:apply-templates select="exsl:node-set($params)/data:params/data:param[@value != ''][@field != 'field']" mode="used_facet_list"/>
      </ul>
    </xsl:if>
   
       <!-- have query, so show results -->
    <xsl:if test="$haveQuery or $haveFacets">
      <div id="resultset">
            <h3> Results</h3>
            <xsl:apply-templates select="result"/>
            </div>
    </xsl:if>
    <!--xsl:copy-of select="/"/-->
    
    <div id="srch_manscrpts"><h3>Search</h3>
    <form method="get" action="{$php_script}">
      <!-- query and field -->
      <p>
        <label for="q">Query</label>
        <input type="text" name="q" id="q" class="keyboardInput" value="{$q}"/>
        
        <label for="field">In</label>
        <select id="field" name="field">
          <xsl:for-each select="exsl:node-set($fields)/data:fields/data:option">
            <option value="{@value}">
              <xsl:if test="@value = $field">
                <xsl:attribute name="selected">selected</xsl:attribute>
              </xsl:if>
              <xsl:value-of select="."/>
            </option>
          </xsl:for-each>
        </select>
      </p>
      
      <!-- document type to search for -->
      <p>
        <label for="doctype">Search for</label>
        <select id="doctype" name="doctype">
          <xsl:for-each select="exsl:node-set($doctypes)/data:doctypes/data:option">
            <option value="{@value}">
              <xsl:if test="@value = $doctype">
                <xsl:attribute name="selected">selected</xsl:attribute>
              </xsl:if>
              <xsl:value-of select="."/>
            </option>
          </xsl:for-each>
        </select>
      </p>
      
      <p>
        <input type="submit" value="Search"/>
        <input type="button" value="Reset" class="reset"/>
      </p>
    </form>
    
    <p>
      <a href="{$php_script}?q=*&amp;field=text&amp;doctype=ms">Browse manuscripts</a>
    </p></div>
    
  </xsl:template>
  
</xsl:stylesheet>
